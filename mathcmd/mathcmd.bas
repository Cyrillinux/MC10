10 K=0:Z$=CHR$(0):W=32:W0=47:W2=25:W6=96:W8=81:W9=58:F$="    "
12 FOR I=1 TO 4:X$(0)=X$(0)+CHR$(134):X$(1)=X$(1)+CHR$(137):NEXT
14 A$=CHR$(140)+CHR$(140):B$=CHR$(136)+"  "+CHR$(132)
16 C$=CHR$(142)+CHR$(132)+CHR$(132)+CHR$(132):L$=CHR$(91):R$=CHR$(93)
20 CLS:PRINT @42,"MATH COMMAND";: PRINT @100,"(C) 2020 CURTIS F KAYLOR";:PRINT @199,"(A)DD,  (S)UBTRACT";
22 PRINT @230,"(M)ULTIPLY, (D)IVIDE";:PRINT @328,"PRESS Q  TO QUIT";:PRINT @463,A$;:PRINT @494,B$;
24 K=ASC(INKEY$+Z$):K=K+W*(K>W6):IF K=81 GOTO 94
26 O=-(K=65)-2*(K=83)-3*(K=77)-4*(K=68):ON -(O=0) GOTO 24:O$=MID$("+-X/",O,1)
30 M=12:D=4:PRINT @32,,,,,,,,,,,,,,,,,,,,,
32 FOR I=0 TO 3:D(I+1)=1:PRINT @482+I*6-6*(I>1),C$;:NEXT 
34 FOR I=0 TO 3:SOUND 128+12*(1ANDI),5:NEXT
40 Q=RND(4):P=28+Q*6-6*(Q>2):N$="":N=0:GOSUB 92:A=RND(9):B=RND(9):C=A:T=A+B:IF O=1 THEN C=A+B
42 IF O=2 THEN A=B+C
44 IF O=3 THEN C=A*B
46 IF O=4 THEN A=B*C
48 M$=RIGHT$(STR$(A),2)+O$+RIGHT$(STR$(B),1):GOSUB 90
50 PRINT @P,M$;:IF N>9 THEN N$="":N=0:GOSUB 92
52 FOR I=1 TO W2-V:K$=INKEY$:K=ASC(K$+Z$):K=K+W*(K>W6):IF K=W8 THEN RUN
54 IF K>W0 AND K<W9 THEN N$=N$+K$:N=VAL(N$):GOSUB 92:IF N=C THEN I=W9
56 NEXT:PRINT@P,F$;:P=P+W:IF I>W9 GOTO 70
58 IF P<480 GOTO 50
60 PRINT @P,L$RIGHT$(STR$(T),2)R$;:FOR I=20 TO 1 STEP -1:SOUND I,1:NEXT:PRINT @P,F$;
62 IF D(Q) THEN D(Q)=0:D=D-1
64 IF D GOTO 72
66 PRINT @235,"GAME  OVER"
68 K$=INKEY$:ON -(K$="")-2*(K$="Q") GOTO 64,94:RUN
70 FOR I=220 TO 190 STEP -3:PRINT @P,X$(I AND 1);:SOUND I,1:NEXT I:PRINT @P,F$;:S=S+T*(V+1)
72 M=M-1:IF M GOTO 40
74 FOR Q=1 TO 4:P=476+Q*6-6*(Q>2):IF D(Q) THEN S=S+100:GOSUB 90:PRINT @P,"****";:SOUND Q*5+1,5
76 PRINT @P,F$;:NEXT Q:V=V+1:GOTO 30
90 PRINT @5,RIGHT$(STR$(M),2)F$RIGHT$(F$+STR$(S),6)F$" Q TO QUIT";:RETURN:REM DISPLAY SCORE
92 PRINT @495,RIGHT$("  "+N$,2);:RETURN:REM DISPLAY ENTERED NUMBER
94 CLS:END
